plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    alias(libs.plugins.kotlin.compose)
}

def enableL8 = getProperty("build.l8.enable").toBoolean()
def enableBaselineProfileGenerator = getProperty("build.baselineprofile.generate.enable").toBoolean()
def enableDexlayoutOptimizer = getProperty("build.dexlayout.optimize.enable").toBoolean()
if (!enableBaselineProfileGenerator && enableDexlayoutOptimizer) {
    logger.warn("Baseline Profile generator was disabled, and cannot enable Dex Layout Optimizer")
    enableDexlayoutOptimizer = false
}

if (enableBaselineProfileGenerator) {
    apply plugin: "androidx.baselineprofile"
}

android {
    namespace = "com.example.myapplication"
    defaultConfig {
        applicationId = "com.example.myapplication"
        versionCode = rootProject.ext.versionCode
        versionName = rootProject.ext.versionName
        compileOptions {
            coreLibraryDesugaringEnabled = enableL8
        }
    }
    buildTypes {
        release {
            if (enableDexlayoutOptimizer) {
                logger.warn("Dex Layout Optimizer was enabled")
            }
            experimentalProperties[
                "android.experimental.r8.dex-startup-optimization"
            ] = enableDexlayoutOptimizer

            postprocessing {
                removeUnusedCode = true
                optimizeCode = true
                obfuscate = true
                removeUnusedResources = true
                proguardFiles file('proguards-rules.pro')
            }

            // baselineprofile plugin does not support postprocessing dsl block, yet.
            // So, we need assign proguard rule again
            // https://issuetracker.google.com/issues/307784512
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), file('proguards/proguard.cfg')
        }
    }

    testFixtures {
        enable = true
    }

    flavorDimensions = ["default"]
    productFlavors {
        develop { }
        beta { }
        staging { }
        production { }
    }
}

dependencies {
    if (enableL8) {
        coreLibraryDesugaring libs.desugar.jdk.libs
    }

    if (enableBaselineProfileGenerator) {
       baselineProfile(project(":tests:baselineprofile"))
    }

    runtimeOnly(androidxLibs.profileinstaller)

    implementation libs.kotlin.stdlib.jdk7
    implementation androidxLibs.annotation
    implementation androidxLibs.activity.ktx
    implementation androidxLibs.appcompat
    implementation androidxLibs.core.ktx
    implementation androidxLibs.constraintlayout
    implementation androidxLibs.fragment.ktx
    implementation androidxLibs.lifecycle.livedata.ktx
    implementation androidxLibs.lifecycle.viewmodel.ktx
    implementation androidxLibs.room.ktx
    implementation androidxLibs.room.runtime

    implementation googleLibs.play.services.osslicenses
}

if (enableBaselineProfileGenerator) {
    baselineProfile {
        variants {
            release {
                automaticGenerationDuringBuild = true
            }
        }
        warnings {
            disabledVariants = false
        }
    }
}
